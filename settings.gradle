pluginManagement {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
}

plugins {
    id 'io.micronaut.build.shared.settings' version '5.3.10'
}

rootProject.name = 'testresources-parent'

def jdbcModules = [
        'core',
        'mysql',
        'mariadb',
        'oracle-xe',
        'postgresql',
        'mssql'
]
def r2dbcModules = [
        'core',
        'mariadb',
        'mysql',
        'oracle-xe',
        'postgresql',
        'mssql',
        'pool'
]

include 'test-resources-bom'
include 'test-resources-build-tools'
include 'test-resources-core'
include 'test-resources-client'
include 'test-resources-elasticsearch'
include 'test-resources-embedded'
include 'test-resources-hivemq'
include 'test-resources-kafka'
include 'test-resources-mongodb'
include 'test-resources-neo4j'
include 'test-resources-oauth2'
include 'test-resources-redis'
include 'test-resources-rabbitmq'
include 'test-resources-server'
include 'test-resources-testcontainers'
include 'test-resources-hashicorp-vault'

jdbcModules.each {
    String projectName = "test-resources-jdbc-$it"
    include projectName
    project(":test-resources-jdbc-$it").projectDir = file("test-resources-jdbc/$projectName")
}
r2dbcModules.each {
    String projectName = "test-resources-r2dbc-$it"
    include projectName
    project(":test-resources-r2dbc-$it").projectDir = file("test-resources-r2dbc/$projectName")
}

micronautBuild {
    importMicronautCatalog()
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
    }
}
