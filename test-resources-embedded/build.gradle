plugins {
    id 'io.micronaut.build.internal.test-resources-module'
    id 'jvm-test-suite'
}

description = """
Provides support for loading test resources resolvers directly within the application.
The resource resolvers are loaded via service loading.
"""

dependencies {
    api(project(':test-resources-core'))
}

testing {
    suites {
        // This new test suite is used to test invalid test resources resolvers.
        // Because the resolvers are loaded from classpath, we have to use a
        // separate source set, otherwise other test suites will fail.
        test2(JvmTestSuite) {
            dependencies {
                implementation(project)
            }
            useJUnitJupiter()
        }
    }
}

configurations {
    test2CompileOnly.extendsFrom(testCompileOnly)
    test2Implementation.extendsFrom(testImplementation)
}

tasks.named("check") {
    dependsOn(testing.suites.test2)
}
